buildscript {
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:7.4.0"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.0"
  }
}

apply plugin: "com.android.library"

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["WaasSdk_" + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["WaasSdk_" + name]).toInteger()
}

android {
  namespace "com.coinbase.waassdk"
  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  mavenCentral()
  google()
}


dependencies {
  implementation 'androidx.annotation:annotation:1.3.0'

  implementation project(':android-native:go-internal-sdk') 
  implementation project(':android-native:mpc-sdk')
  implementation 'androidx.appcompat:appcompat:1.6.1'
}

task goSdkVersion {
    doLast {
        println "${project.property("WaasSdk_goInternalSdkVersion")}"
    }
}

task mpcUtilsVersion {
    doLast {
        println "${project.property("WaasSdk_mpcSdkVersion")}"
    }
}
